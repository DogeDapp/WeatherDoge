def gitRev = {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return 'nogit';
    }
}

Closure<String[]> getVersionDef = {
    new File('WeatherDoge/versionDef.txt').text.trim().split('\\r?\\n')
}

Closure<String> getVersionName = { flavor ->
    getVersionDef()[1] + flavor + '-' + gitRev()
}

apply plugin: 'com.android.application'
apply from: 'signing.gradle'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode getVersionDef()[0].toInteger()
        buildConfigField 'String', 'FLAVOR_PLAY', '"play"'
        buildConfigField 'String', 'FLAVOR_FOSS', '"foss"'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    productFlavors {
        play {
            versionName getVersionName('p')
        }
        foss {
            versionName getVersionName('f')
        }
    }
}

dependencies {
    playCompile 'com.google.android.gms:play-services-location:8.4.0'
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'com.android.support:support-v4:23.1.1'
}
