def gitRev = {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return 'nogit';
    }
}

Closure<String[]> getVersionDef = {
    new File('WeatherDoge/versionDef.txt').text.trim().split('\\r?\\n')
}

Closure<String> getVersionName = { flavor ->
    getVersionDef()[1] + flavor + '-' + gitRev()
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.1'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        applicationId "com.versobit.weatherdoge"
        versionCode getVersionDef()[0].toInteger()
        buildConfigField 'String', 'FLAVOR_PLAY', '"play"'
        buildConfigField 'String', 'FLAVOR_FOSS', '"foss"'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    productFlavors {
        play {
            versionName getVersionName('p')
        }
        foss {
            applicationId "com.versobit.weatherdoge.foss"
            versionName getVersionName('f')
        }
    }
}

dependencies {
    playCompile 'com.google.android.gms:play-services-location:9.4.0'
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'com.android.support:support-v4:24.1.1'
    compile 'com.android.support:design:24.1.1'
    compile 'com.getbase:floatingactionbutton:1.10.1'
    compile 'com.plattysoft.leonids:LeonidsLib:1.3.1'
}

// Find and apply the signing file
// See signing.template.gradle for more information
if (project.hasProperty('signing.file')) {
    if (new File(project.property('signing.file') as String).exists()) {
        apply from: project.property('signing.file')
    } else {
        throw new InvalidUserDataException('Signing configuration file does not exist: ' +
                project.property('signing.file'))
    }
} else {
    logger.log(LogLevel.WARN, 'WARNING: No signing configuration file defined.')
}
