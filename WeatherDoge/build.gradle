def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine './versionCode.sh'
            standardOutput = stdout
        }
        return stdout.toString().trim().toInteger()
    }
    catch (ignored) {
        return null;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine './versionName.sh'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}

apply plugin: 'com.android.application'
apply from: 'signing.gradle'

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        versionCode getVersionCode()
        versionName getVersionName()
        buildConfigField "String", "FLAVOR_PLAY", '"play"'
        buildConfigField "String", "FLAVOR_FOSS", '"foss"'
    }
    packagingOptions {
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/NOTICE.txt"
    }
    productFlavors {
        play
        foss
    }
}

dependencies {
    playCompile 'com.google.android.gms:play-services-location:7.3.0'
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-lang3:3.3.2'
}
